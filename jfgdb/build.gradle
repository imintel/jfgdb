import com.amazonaws.services.s3.transfer.*
import com.amazonaws.auth.*
import com.amazonaws.events.*
import js.classmethod.aws.gradle.s3.*

/*
 Java FileGeodatabase Thin driver
 */

description = """Rough ESRI File Geodatabase Java API"""

buildscript {
  repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    if (System.getProperty("java.class.version") >= "52") {
        classpath "jp.classmethod.aws:gradle-aws-plugin:0.+"
       
    }
	classpath 'com.amazonaws:aws-java-sdk:1.10.56'
	classpath 'org.ini4j:ini4j:0.5.1'
	classpath 'net.java.dev.jna:jna:4.2.1'
  }
}


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

if (System.getProperty("java.class.version") >= "52") {
    apply plugin: "jp.classmethod.aws.s3"

    aws {
      profileName = 'default'
      region = 'Ireland'
    }

}

compileJava {
	options.fork = true
	options.forkOptions.executable = 'javac'
	options.compilerArgs << '-XDignore.symbol.file'
	sourceCompatibility = 1.7
}

repositories {
	mavenLocal()
	maven { url "http://repo.maven.apache.org/maven2" }
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
				url "s3://jfgdb/maven2"
				
				
				def f = new File(System.getProperty("user.home"), ".aws")
				def credentialfile = new File(f, "credentials")
				if (credentialfile.exists()) {
					org.ini4j.Ini ini = new org.ini4j.Ini(credentialfile);
					java.util.prefs.Preferences prefs = new org.ini4j.IniPreferences(ini);
					def id = prefs.node("default").get("aws_access_key_id", null)
					def sk = prefs.node("default").get("aws_secret_access_key", null)
					
					if (id != null && sk != null) {
						System.out.println("access key read")
						credentials(AwsCredentials) {
							accessKey id
							secretKey sk
						}
					} else 
					{
						System.out.println("no access keys read")
					}
				}
				
		}
	}
}

dependencies {
	compile group: 'com.esri.geometry', name: 'esri-geometry-api', version:'1.2.1'
	compile group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.moxy', version:'2.5.0'
	compile 'net.java.dev.jna:jna:4.2.1'
	testCompile group: 'junit', name: 'junit', version:'4.11'
}


////////////////////////////////////////////////////////////////////////
// binaries management - sync binaries into S3 folder - download all libraries from S3


def rootMainResources = new File(project.buildDir , "/resources/main")
def rootOutputLibs = new File(rootMainResources, "sharedlibraries/" )
def outputlibsdir = new File(rootOutputLibs, 
			"${com.sun.jna.Platform.RESOURCE_PREFIX}")

println "Output libs dir :$outputlibsdir"
if (!outputlibsdir.exists()) 
{
    outputlibsdir.mkdirs()
}


if (System.getenv("ESRI_FILE_GDB_HOME") != null) {

task copySharedLibsInResources << {
	
   copy { 
       into(outputlibsdir)
       from( "../nativewrapper/build/binaries/fGDBJNIWrapperSharedLibrary") 
           include("FGDB*.dll")
           include("*.so")
	       include("*.dylib")
   }
   assert  System.getenv("ESRI_FILE_GDB_HOME") != null 
   assert  new File(System.getenv("ESRI_FILE_GDB_HOME")).exists()
   
   copy {
       into(outputlibsdir)     
       from( new File(System.getenv("ESRI_FILE_GDB_HOME") , "bin64")) 
           include("FileGDBAPI.dll")
           exclude("*.pdb")
           exclude("*D.dll")
       
   }

   copy {
   into(outputlibsdir)     
   from( System.getenv("ESRI_FILE_GDB_HOME") + "/lib") 
       include("*.so")
	   include("*.dylib")
   
   }

   copy {
    into(rootMainResources)     
    from ("src/main/resources")  
        include("**/*/files")
    
   }

    /*
        we can't do it automatically, must be manual to solve
        dll loading dependencies

   doLast {
        String l = (outputlibsdir.listFiles().collect { it.name }).join(",")
        new File(outputlibsdir, "files").newWriter().write(l) 
   }
   */

}

}


if (System.getProperty("java.class.version") >= "52") {
	
	// sync only available on java 8
    task syncBinariesObjects(type: jp.classmethod.aws.gradle.s3.SyncTask) {
      bucketName 'jfgdb'
      source file(rootOutputLibs)
      prefix "sharedlibraries/"
    }

    if (System.getenv("ESRI_FILE_GDB_HOME") != null) {
    syncBinariesObjects.dependsOn copySharedLibsInResources
	}
   
}




task createVersionPropertyFile << {
    def versionProp = new java.util.Properties()
    versionProp.setProperty("version", project.version)
    def resourcemain = new File(project.buildDir, "resources/main")
    resourcemain.mkdirs()
    versionProp.save(new FileOutputStream(new File(resourcemain,"fgdbversion.properties")), "jfgdbapi version")
}

if (System.getenv("ESRI_FILE_GDB_HOME") != null) {
	compileJava.dependsOn copySharedLibsInResources

}

compileJava.dependsOn createVersionPropertyFile

